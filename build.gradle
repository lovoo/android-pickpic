// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.50'
    ext.multidex_version = "2.0.1"

    ext.legacy_v4_version = "1.0.0"
    ext.app_compat_version = "1.1.0"
    ext.material_version = "1.2.0-alpha02"
    ext.vector_drawable_version = "1.0.0"
    ext.constraint_layout_version = '1.1.3'
    ext.recycler_view_version = "1.1.0"
    ext.card_view_version = "1.0.0"
    ext.browser_version = "1.0.0"
    ext.exif_version = "1.1.0"
    ext.media_version = "1.1.0"
    ext.arch_version = "2.1.0"
    ext.arch_worker_version = "1.0.1"

    ext.rx_java_version = '2.2.15'
    ext.rx_android_version = '2.1.1'
    ext.glide_version = '4.10.0'
    ext.gson_version = '2.8.6'
    ext.facebook_version = '5.11.2'

    ext.espresso_version = "3.2.0"
    ext.uiautomator_version = "2.2.0"
    ext.orchestrator_version = "1.2.0"
    ext.androidX_test_version = "1.2.0"

    ext.target_version = 28
    ext.min_version = 18

    def version = System.getenv("VERSION")
    def time = System.currentTimeMillis()

    ext.version_name = version == null ? "$time-SNAPSHOT" : version
    ext.version_code = 1

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.9.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.3'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.18"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    setGroup('com.lovoo.android')
    setVersion(version_name)

    repositories {
        google()
        jcenter()
    }

    if (it.name != 'app') {
        apply plugin: 'maven-publish'
        apply plugin: 'com.jfrog.bintray' // --> external deploy
        apply plugin: 'com.jfrog.artifactory' // --> internal deploy
        apply plugin: 'org.jetbrains.dokka'
        dokka {
            outputFormat = 'gfm'
            outputDirectory = "$projectDir/javadoc"
            jdkVersion = 8
            includeNonPublic = false
            reportUndocumented = false

            linkMapping {
                // Unix based directory relative path to the root of the project (where you execute gradle respectively).
                dir = "./"

                // URL showing where the source code can be accessed through the web browser
                url = "https://github.com/lovoo/android-pickpic/blob/master"

                // Suffix which is used to append the line number to the URL. Use #L for GitHub
                suffix = "#L"
            }
        }
    }
}

// file will be deleted before public deploy
if (file('deploy-internal.gradle').exists()) {
    apply from: 'deploy-internal.gradle'
}

if (file('deploy-external.gradle').exists()) {
    apply from: 'deploy-external.gradle'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task names {
    print(project.getName())
}